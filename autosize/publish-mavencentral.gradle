apply plugin: 'maven-publish'
apply plugin: 'signing'

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.source

    exclude "**/R.class"  //排除`R.class`
    exclude "**/BuildConfig.class"  //排除`BuildConfig.class`
}

//--- 修改配置一 ---
ext {
    PUBLISH_GROUP_ID = 'io.github.victoryckl'
    PUBLISH_ARTIFACT_ID = 'autosize'
    PUBLISH_VERSION = rootProject.versionName
}

//--- 修改配置二 ---
println('-----------')
ext["signing.keyId"] = ''    // 刚才获取的gpg秘钥后8位
ext["signing.password"] = '' // 创建GPG秘钥时设置的密码
ext["signing.secretKeyRingFile"] = 'path/to/xxx.gpg' // 生成的.gpg结尾的密钥文件路径
ext["ossrhUsername"] = ''   // sonatype用户名
ext["ossrhPassword"] = ''   // sonatype密码
ext["sonatype.email"] = ''  // sonatype注册的email

def extVar = ext

//File secretPropsFile = project.rootProject.file('maven-publish.properties')
File secretPropsFile = new File('e:/develop/key-store/gpg/maven-publish.properties')
if (secretPropsFile.exists()) {
    println "Found secret props file, loading props: ${secretPropsFile.path}"

    Properties p = new Properties()
    p.load(new FileInputStream(secretPropsFile))
    p.each { name, value ->
        extVar[name] = p.getProperty(name)
        //println("${name} ${extVar[name]}")
    }
} else {
    def msg = "No props file: ${secretPropsFile.path}"
    println msg
    throw new RuntimeException(msg)
}

publishing {
    publications {
        release(MavenPublication) {
            println("publish-maven Log-------> PUBLISH_GROUP_ID: $PUBLISH_GROUP_ID; PUBLISH_ARTIFACT_ID: $PUBLISH_ARTIFACT_ID; PUBLISH_VERSION: $PUBLISH_VERSION")
            // The coordinates of the library, being set from variables that
            // we'll set up in a moment

            //配置一传入的参数
            groupId PUBLISH_GROUP_ID
            artifactId PUBLISH_ARTIFACT_ID
            version PUBLISH_VERSION

            // Two artifacts, the `aar` and the sources
            artifact("$buildDir/outputs/aar/${project.getName()}-release.aar")
            artifact androidSourcesJar

            // Self-explanatory metadata for the most part
            pom {
                //--- 修改配置三 ---
                name = PUBLISH_ARTIFACT_ID
                description = '一个极低成本的 Android 屏幕适配方案' //添加文件描述
                // If your project has a dedicated site, use its URL here
                url = 'https://github.com/victoryckl/AndroidAutoSize' //项目github链接
                licenses {
                    license {
                        //协议类型，一般默认Apache License2.0的话不用改：
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        //--- 修改配置四 ---
                        id = extVar["ossrhUsername"]     //你的sonatype用户ID
                        name = extVar["ossrhUsername"]   //你的sonatype用户名
                        email = extVar["sonatype.email"]    //你的sonatype注册邮箱
                    }
                }
                // Version control info, if you're using GitHub, follow the format as seen here
                scm {
                    //--- 修改配置五 ---
                    //修改成你的Git地址：
                    connection = 'scm:git:github.com/victoryckl/AndroidAutoSize.git'
                    developerConnection = 'scm:git:ssh://github.com/victoryckl/AndroidAutoSize.git'
                    //分支地址：
                    url = 'https://github.com/victoryckl/AndroidAutoSize/tree/master'
                }
                // A slightly hacky fix so that your POM will include any transitive dependencies
                // that your library builds upon
                withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')

                    project.configurations.implementation.allDependencies.each {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
    repositories {
        // The repository to publish to, Sonatype/MavenCentral
        maven {
            // This is an arbitrary name, you may also use "mavencentral" or
            // any other name that's descriptive for you

            //--- 修改配置六 ---
            name = "AndroidAutoSize"
            def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            // You only need this if you want to publish snapshots, otherwise just set the URL
            // to the release repo directly
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

            // The username and password we've fetched earlier
            credentials {
                username extVar["ossrhUsername"]
                password extVar["ossrhPassword"]
            }
        }
    }
}
signing {
    sign publishing.publications
}